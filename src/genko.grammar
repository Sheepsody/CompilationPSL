// Lexing

cons = { pi }
     pi = @{ "PI" }

uint = { ASCII_DIGIT+ }
int = { ("-" | "+")? ~ uint }
num = @{ int ~ ("." ~ uint)? ~ ("e" ~ int)? }

bool = @{ "true" | "false" }

operator = _{ add | sub | mul | div | pow | eq | le | ge | lt | gt | and | or}
    add      = @{ "+" }
    sub = @{ "-" }
    mul = @{ "*" }
    div   = @{ "/" }
    pow    = @{ "^" }
    eq     = @{ "==" }
    le     = @{ "<=" }
    ge     = @{ ">=" }
    lt     = @{ "<" }
    gt     = @{ ">" }
    and     = @{ "&&" }
    or     = @{ "||" }

function = _{ cos }
    cos      = @{ "cos" }

// FIXME: Problème avec les constantes définies telles quelles....
ident = @{ ASCII_ALPHA+ }

// Parsing

init = { ident ~ ":=" ~ expr }

binary = { term ~ (operator ~ term)*}
unary = { operator ~ term }
call = { function ~ "(" ~ expr ~ ")" }

term = _{ num | bool | cons | "(" ~ expr ~ ")" | call | ident } // Warning! ident at the end
expr = _{ binary | unary | call }


exprast = { expr }
item = _{ init | exprast }

program = _{ SOI ~ item ~ (";" ~ item)* ~ ";"? ~ EOI }

// Implicit whitespace & comment

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
