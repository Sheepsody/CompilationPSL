////////////
// Lexing //
////////////

uint = { ASCII_DIGIT+ }
int = { ("-" | "+")? ~ uint }
num = @{ int ~ ("." ~ uint)? ~ ("e" ~ int)? }

bool = @{ "true" | "false" }

operator = _{ add | sub | mul | div | pow | eq | le | ge | lt | gt | and | or}
    add      = @{ "+" }
    sub = @{ "-" }
    mul = @{ "*" }
    div   = @{ "/" }
    pow    = @{ "^" }
    eq     = @{ "==" }
    le     = @{ "<=" }
    ge     = @{ ">=" }
    lt     = @{ "<" }
    gt     = @{ ">" }
    and     = @{ "&&" }
    or     = @{ "||" }

let_ = _{ "let" }
func_ = _{ "fn" }

ident = @{ ASCII_ALPHA+ }

/////////////
// Parsing //
/////////////

initexpr = { let_ ~ ident ~ "=" ~ expr }
assignexpr = { ident ~ "=" ~ expr }
protoexpr = { "(" ~ (ident ~ ("," ~ ident)* )? ~ ")" }
funcexpr = { func_ ~ ident ~ protoexpr ~ blockexpr }

binaryexpr = { term ~ (operator ~ term)*}
unaryexpr = { operator ~ term }
call = { ident ~ "(" ~ expr ~ ")" }

term = _{ num | bool | "(" ~ expr ~ ")" | call | ident } // Warning! ident at the end
expr = _{ binaryexpr | unaryexpr | call }


line = _{ initexpr | assignexpr | blockexpr | funcexpr | expr }

lines = _{ (line ~ ";")+ }
program = { SOI ~ lines ~ EOI }

blockexpr = { "{" ~ lines? ~ "}" }

// Implicit whitespace & comment

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
